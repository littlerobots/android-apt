apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile 'com.android.tools.build:gradle:1.3.0'
    testCompile 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.6'
}

version = "1.9-SNAPSHOT"

sourceCompatibility = 1.6
targetCompatibility = 1.6

ext {
    artifactId = 'android-apt'
}

group = 'com.neenbedankt.gradle.plugins'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from groovydoc
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

if (project.hasProperty("signing.keyId")) {
    signing {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment { deployment -> signing.signPom(deployment) }

            if (project.hasProperty('sonatypeUsername')) {
                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }
            uniqueVersion = true

            pom.project {

                parent {
                    groupId 'org.sonatype.oss'
                    artifactId 'oss-parent'
                    version '7'
                }

                name 'Gradle android-apt plugin'
                packaging 'jar'
                description 'A Gradle plugin that ensures Android Studio will pick up generated source from annotation processors and provides an apt configuration for excluding annotation processors from runtime dependencies.'
                url 'https://bitbucket.org/hvisser/android-apt'

                scm {
                    url 'https://bitbucket.org/hvisser/android-apt'
                    connection 'scm:hg:https://bitbucket.org/hvisser/android-apt'
                    developerConnection 'scm:hg:https://bitbucket.org/hvisser/android-apt'
                }

                licenses {
                    license {
                        name 'Unlicense'
                        url 'http://unlicense.org/UNLICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'hvisser'
                        name 'Hugo Visser'
                    }
                }
            }
        }
    }
}
